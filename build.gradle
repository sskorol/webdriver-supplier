plugins {
    id "java-library"
    id "java"
    id 'ru.vyarus.quality' version '4.7.0'
    id "jacoco"
    id "maven-publish"
    id "signing"
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.42.0"
}

group 'io.github.sskorol'
version = project['version']
description 'WebDriver Supplier library, which helps to effectively manage web browser sessions.'

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle".toString()
    projectUrl = "https://github.com/sskorol/webdriver-supplier"
    moduleName = "io.github.sskorol.webdriversupplier"
    lombokVersion = "1.18.24"
}

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    compileOnly('com.google.code.findbugs:findbugs-annotations:3.0.1')
    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    implementation(
            'org.testng:testng:7.6.0',
            'org.jooq:joor:0.9.14',
            'one.util:streamex:0.8.1',
            'io.vavr:vavr:0.10.4',
            'org.seleniumhq.selenium:selenium-java:4.1.4',
            'org.bouncycastle:bcprov-jdk15on:1.70',
            'com.fasterxml.jackson.core:jackson-databind:2.13.3',
            'io.github.bonigarcia:webdrivermanager:5.1.1',
            'org.aeonbits.owner:owner:1.0.12',
            'commons-io:commons-io:2.11.0',
            'org.apache.commons:commons-lang3:3.12.0',
            'io.github.sskorol:cdt-java-client:5.0.0'
    )
    testImplementation(
            'org.assertj:assertj-core:3.22.0',
            'org.mockito:mockito-inline:4.5.1',
            'org.mockito:mockito-core:4.5.1'
    )
}

if (project.hasProperty('release')) {
    apply from: "${gradleScriptDir}/maven-publish.gradle"
}

jacoco.toolVersion = "0.8.8"
jacocoTestReport {
    reports {
        xml.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/jacoco.xml")
        html.outputLocation = file("${buildDir}/reports/jacoco")
    }
}

tasks.named('wrapper') {
    gradleVersion = '7.4.2'
}

test {
    useTestNG() {
        suites 'src/test/resources/smoke-suite.xml'
    }

    doFirst {
        jvmArgs = ['--add-opens', 'java.base/java.lang.invoke=ALL-UNNAMED']
    }
}
