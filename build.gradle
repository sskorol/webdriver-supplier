plugins {
    id 'ru.vyarus.quality' version '4.3.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

group 'io.github.sskorol'
version = version
description 'WebDriver Supplier library, which helps to effectively manage web browser sessions.'

ext {
    gradleScriptDir = "${rootProject.projectDir}/gradle"
    projectUrl = "https://github.com/sskorol/webdriver-supplier"
    lombokVersion = '1.18.12'
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply from: "${gradleScriptDir}/bintray.gradle"
apply from: "${gradleScriptDir}/release.gradle"
apply from: "${gradleScriptDir}/maven-publish.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

dependencies {
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    implementation('org.testng:testng:7.2.0',
            'org.jooq:joor-java-8:0.9.12',
            'one.util:streamex:0.7.2',
            'io.vavr:vavr:1.0.0-alpha-3',
            'org.slf4j:slf4j-simple:1.8.0-beta4',
            'org.seleniumhq.selenium:selenium-java:3.141.59',
            'io.github.bonigarcia:webdrivermanager:4.1.0',
            'org.aeonbits.owner:owner:1.0.12',
            'com.google.guava:guava:25.1-jre'
    )
    testImplementation('org.assertj:assertj-core:3.16.1',
            'org.mockito:mockito-core:2.12.0',
            'org.powermock:powermock-api-mockito2:2.0.7',
            'org.powermock:powermock-module-testng:2.0.7'
    )
}

task sourceJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allJava
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    getArchiveClassifier().set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

test {
    useTestNG() {
        suites 'src/test/resources/smoke-suite.xml'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
